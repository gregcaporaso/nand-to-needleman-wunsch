// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoToThe;

    /** Initializes the library. */
    function void init() {
        let twoToThe = Array.new(15);
        let twoToThe[0] = 1;
        let twoToThe[1] = 2;
        let twoToThe[2] = 4;
        let twoToThe[3] = 8;
        let twoToThe[4] = 16;
        let twoToThe[5] = 32;
        let twoToThe[6] = 64;
        let twoToThe[7] = 128;
        let twoToThe[8] = 256;
        let twoToThe[9] = 512;
        let twoToThe[10] = 1024;
        let twoToThe[11] = 2048;
        let twoToThe[12] = 4096;
        let twoToThe[13] = 8192;
        let twoToThe[14] = 16384;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }

        return x;
    }

    function boolean _bit(int x, int i) {
        return (x & twoToThe[i]) > 0;
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int i, shiftedx, result;
        var boolean negate;

        let negate = false;
        if ((x < 0) | (y < 0)) {
            if ((x < 0) & (y < 0)) {
                let negate = false;
            } else {
                let negate = true;
            }
            let x = Math.abs(x);
            let y = Math.abs(y);
        }

        let i = 0;
        let shiftedx = x;
        let result = 0;

        while (i < 16) {
            if (Math._bit(y, i)) {
                let result = result + shiftedx;
            }
            let shiftedx = shiftedx + shiftedx;
            let i = i + 1;
        }

        if (negate) {
            let result = -result;
        }

        return result;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        // I found this discussion of the derivation of this function
        //  extremely helpful: https://bit.ly/3UmEdFL
        var int q;
        var boolean negate;

        let negate = false;
        if ((x < 0) | (y < 0)) {
            if ((x < 0) & (y < 0)) {
                let negate = false;
            } else {
                let negate = true;
            }
            let x = Math.abs(x);
            let y = Math.abs(y);
        }


        if (y > x) {
            let q = 0;
            return q;
        }

        if (y = x) {
            let q = 1;
            return q;
        }

        // check if y*2 overflows
        if ((y + y) < 0) {
            let q = 0;
            return q;
        }

        let q = Math.divide(x, y + y);

        if (x - (q * (y + y)) < y) {
            let q = q + q;
        } else {
            let q = q + q + 1;
        }

        if (negate) {
            let q = -q;
        }

        return q;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int i, temp, result;

        if (x < 0) {
            do Sys.error(4);
        }

        let result = 0;
        let i = 7; // (bit width / 2) - 1;

        while (i > -1) {
            let temp = result + twoToThe[i];
            let temp = temp * temp;
            if (((temp < x) | (temp = x)) & (temp > 0)) {
                let result = result + twoToThe[i];
            }
            let i = i - 1;
        }
        return result;

    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a < b) {
            return b;
        }
        return a;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (b < a) {
            return b;
        }
        return a;
    }
}
